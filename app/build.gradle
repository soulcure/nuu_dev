apply plugin: 'com.android.application'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        applicationId "com.nuu.nuuinfo"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode 6
        versionName "6.0"

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file('keystore/nuu_info_keystore.jks')
            storePassword '12345678910'
            keyAlias 'key0'
            keyPassword '12345678910'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }


//指定打包后应用名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "nuuinfo_${releaseTime()}.apk"
            } else {
                outputFileName = "nuuinfo_debug_${releaseTime()}.apk"
            }

            variant.getAssembleProvider().configure() {
                it.doLast {
                    copyBuildApk()
                }
            }

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
dependencies {
    compileOnly files('libs/framework.jar')

    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:design:${SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.protobuf:protobuf-java:3.4.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.github.blikoon:QRCodeScanner:0.1.2'

    implementation('com.paypal.sdk:paypal-android-sdk:2.16.0')

    implementation 'pub.devrel:easypermissions:0.4.0' //easy permissions
}

project.afterEvaluate {
    //handleBuildTag()
}

def copyBuildApk() {
    copy {
        from("${buildDir}/outputs/apk/release") {
            include "*.apk"
        }
        into '../dist'
    }
}


def static releaseTime() {
    return new Date().format("MMdd_HHmm")
}
